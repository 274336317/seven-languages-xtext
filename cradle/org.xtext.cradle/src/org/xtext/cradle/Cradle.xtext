grammar org.xtext.cradle.Cradle with org.eclipse.xtext.xbase.Xbase

generate cradle "http://www.cradle.org/language/Cradle"

CradleFile:
	imports+=ImportDeclaration*
	declarations+=Declaration*;
	
ImportDeclaration :
	'import' importedNamespace=QualifiedNameWithWildCard
;

QualifiedNameWithWildCard:
	ID ('.' ID)* '.*'?
;

Declaration:
	Task | Parameter
;

Parameter:
	'param' (type=JvmTypeReference)? name=ValidID ('=' init=XExpression )?
;
	
Task:
	'task' name=ValidID ( ('depends' 'on' | ':') dependsOn+=TaskReference (',' dependsOn+=TaskReference)*)?
		action=XBlockExpression
;

TaskReference:
	reference=[Task]
	| softReference=STRING
;

